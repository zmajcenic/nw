10 REM CONSOLE TEST IN SCREEN 2
20 KEYOFF:COLOR 15,1,4:SCREEN 2:DEFINT A-Z
30 LINE (0,0)-(255,127),4,BF

100 REM COPY CHARACTER SET FROM ROM TO LOWER SCREEN THIRD AND SET COLOR
110 X=256*PEEK(5)+PEEK(4)+33*8:REM FONT FROM CHARACTER 33
120 Y=BASE(12)+4096+33*8
130 FOR I=0 TO 759:VPOKE Y+I,PEEK (X+I):NEXT
140 Z=BASE(11)+4096
150 FOR I=0 TO 2047:VPOKE Z+I,&H1F:NEXT

200 REM FAKE SCREEN 1 MODE, SET WIDTH 30, 6 LINES SCREEN HEIGHT AND NAME TABLE BEGIN
210 POKE &HFCAF,1:REM SCREEN MODE
220 POKE &HF3AF,30:REM LINL32
230 POKE &HF3B0,30:REM LINLEN
240 POKE &HF3B1,6:REM CRTCNT
250 Y=BASE(10)+512+2*32:REM SCREEN 2 NAME TABLE BOTTOM THIRD PLUS 2 ROWS
260 POKE &HF922,Y MOD 256
270 POKE &HF3BD,Y MOD 256
280 POKE &HF923,Y\256
290 POKE &HF9BE,Y\256
300 CLS:Y=BASE(10)+512:FOR I=0 TO 63:VPOKE Y+I,32:NEXT
310 REM DO FIXES REGARDING CURSOR
320 VPOKE BASE(10)+511,254:REM SEEMS TO WRITE 255'S INTO PATTERN FOR CHAR 511, HIDE IT
330 REM FILL PATTERN OF CURSOR CHARACTER
340 VPOKE BASE(12)+6143,255

400 REM DEFINE CHARACTER 0 - LEFT GRID
410 Y=BASE(12)+4096:RESTORE 50370
420 FOR I=0 TO 7:READ A: VPOKE Y+I,A:NEXT
430 REM DEFINE CHARACTER 31 - RIGHT GRID
440 Y=BASE(12)+4096+31*8
450 FOR I=0 TO 7:READ A: VPOKE Y+I,A:NEXT
460 REM DRAW GRID
470 Y=BASE(10)+512
480 FOR I=0 TO 7:VPOKE Y+I*32,0:VPOKE Y+I*32+31,31:NEXT

500 REM DEFINE ACTION BUTTONS
510 RESTORE 50020
520 BN=5:K=255-4*BN:Y=BASE(12)+4096+8*K
530 FOR I=0 TO BN*32-1:READ A:VPOKE Y+I,A:NEXT
540 Y=BASE(10)+513
550 FOR L=0 TO BN-1:FOR J=0 TO 1:FOR I=0 TO 1
560 VPOKE Y+L*2+J+I*32,K:K=K+1
570 NEXT I:NEXT J:NEXT L

600 PRINT "Enter command ";:INPUT A$
610 IF A$="EXIT" OR A$="exit" THEN END
620 PRINT A$
630 GOTO 600

10000 A$=INKEY$:IF A$="" GOTO 10000

50000 ' --- factory
50010 ' color 1
50020 DATA &HFF,&H00,&H30,&H30,&H31,&H33,&H3F,&H3F
50030 DATA &H32,&H35,&H37,&H37,&H3F,&H00,&HFF,&H00
50040 DATA &HFF,&H00,&H00,&H00,&H24,&H6C,&HFC,&HFC
50050 DATA &H7C,&H7C,&H7C,&H7C,&HFC,&H00,&HFF,&H00
50060 ' 
50070 ' --- rocket
50080 ' color 1
50090 DATA &HFF,&H80,&H80,&H80,&H80,&H81,&H83,&H87
50100 DATA &H9F,&HA7,&H8A,&H92,&H84,&H80,&HFF,&H00
50110 DATA &HFF,&H00,&H00,&H70,&HF0,&HB0,&H60,&HC0
50120 DATA &H80,&H1C,&H14,&H1C,&H14,&H00,&HFF,&H00
50130 ' 
50140 ' --- satellite
50150 ' color 1
50160 DATA &HFF,&H80,&H80,&H80,&H80,&H80,&H80,&H81
50170 DATA &H85,&H8E,&H9B,&HB6,&H9C,&H88,&HFF,&H00
50180 DATA &HFF,&H00,&H10,&H38,&H6C,&HD8,&H70,&HA0
50190 DATA &H80,&H0C,&H0A,&H0A,&H0C,&H00,&HFF,&H00
50200 ' 
50210 ' --- propaganda
50220 ' color 1
50230 DATA &HFF,&H84,&H92,&H88,&H84,&HA1,&H9B,&H81
50240 DATA &H98,&HA2,&H84,&H88,&H81,&H81,&HFF,&H00
50250 DATA &HFF,&H90,&HA4,&H08,&H90,&HC2,&HEC,&HC0
50260 DATA &H88,&HA6,&H95,&H86,&HC4,&HC0,&HFF,&H00
50270 ' 
50280 ' --- Launch
50290 ' color 1
50300 DATA &HFF,&H80,&H87,&H88,&H93,&HA7,&HAF,&HAF
50310 DATA &HAF,&HA7,&H93,&H88,&H87,&H80,&HFF,&H00
50320 DATA &HFF,&H00,&HC0,&H20,&H90,&HC8,&HE8,&HE8
50330 DATA &HE8,&HC8,&H94,&H24,&HC6,&H00,&HFF,&H00
50340 ' 
50350 ' --- grid
50360 ' color 1
50370 DATA &HFE,&H82,&HA2,&H82,&H82,&H82,&H8A,&H82
50380 DATA &H7F,&H41,&H51,&H41,&H41,&H41,&H45,&H41


