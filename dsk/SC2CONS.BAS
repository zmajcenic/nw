10 REM CONSOLE TEST IN SCREEN 2
20 KEYOFF:COLOR 15,1,4:SCREEN 2:DEFINT A-Z
30 LINE (0,0)-(255,127),4,BF

100 REM COPY CHARACTER SET FROM ROM TO LOWER SCREEN THIRD AND SET COLOR
110 X=256*PEEK(5)+PEEK(4):REM FONT
120 Y=BASE(12)+4096:Z=BASE(11)+4096
130 FOR I=0 TO 2047:VPOKE Y+I,PEEK (X+I):VPOKE Z+I,&H1F:NEXT

200 REM FAKE SCREEN 1 MODE, SET WIDTH 30, 6 LINES SCREEN HEIGHT AND NAME TABLE BEGIN
210 POKE &HFCAF,1:REM SCREEN MODE
220 POKE &HF3AF,30:REM LINL32
230 POKE &HF3B0,30:REM LINLEN
240 POKE &HF3B1,6:REM CRTCNT
250 Y=BASE(10)+512+2*32:REM SCREEN 2 NAME TABLE BOTTOM THIRD PLUS 2 ROWS
260 POKE &HF922,Y MOD 256
270 POKE &HF3BD,Y MOD 256
280 POKE &HF923,Y\256
290 POKE &HF9BE,Y\256
300 CLS:Y=BASE(10)+512:FOR I=0 TO 63:VPOKE Y+I,32:NEXT
310 REM DO FIXES REGARDING CURSOR
320 VPOKE BASE(10)+511,254:REM SEEMS TO WRITE 255'S INTO PATTERN FOR CHAR 511, HIDE IT
330 REM FILL PATTERN OF CURSOR CHARACTER
340 VPOKE BASE(12)+6143,255

400 REM DEFINE CHARACTER 0
410 Y=BASE(12)+4096
420 FOR I=0 TO 7:READ A: VPOKE Y+I,A:NEXT
430 REM DRAW GRID
440 Y=BASE(10)+512
450 FOR I=0 TO 7:VPOKE Y+I*32,0:VPOKE Y+I*32+31,0:NEXT

600 PRINT "Enter command:":INPUT A$
610 IF A$="EXIT" OR A$="exit" THEN END
620 PRINT A$
630 GOTO 600

10000 A$=INKEY$:IF A$="" GOTO 10000

50000 DATA &HFE,&H82,&HA2,&H82,&H82,&H82,&H8A,&H82